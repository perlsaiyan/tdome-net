#NOP -- Communications
#nop -- vim: syntax=tt

#class {comms} {kill}
#class {comms} {open}

#var commLogFile {/tmp/ram/tdome-comms}
#var commLogState 1;

#var commsLine[1] {};
#var commColors[gossip] {<bfb>};
#var commColors[tele]   {<fbb>};
#var commColors[gang]   {<def>};
#var commColors[bj]     {<bcf>};

#format {screenwidth} {%C};

#nop -- Change this to a list you lazy, lazy bastard
#var commsIgnore[Richard] 1;
#var commsIgnore[Beavis] 1;
#var commsIgnore[Thundarr the Barbarian] 1;
#var commsIgnore[Ice T] 1;
#var commsIgnore[Butthead] 1;
#var commsIgnore[Lag Man] 1;

#var SOMEONE {Someone};

#action {%1 gossip{s|}, '%3} {
  #if {&{$commsIgnore[%1]}} {#nop } {
    globalCommsListAdd {gossip} {%1} {%3};
    #if { $commLogState } {#line {log} {$commLogFile} }
  }
};

#action {%1 telepaths to you '%2} {
  globalCommsListAdd {tele} {[$playerInitial[$name]] %1} {%2};
    #if { $commLogState } {#line {log} {$commLogFile} }
};
#action {%1 replies '%2} {
  globalCommsListAdd {tele} {[$playerInitial[$name]]%1} {%2};
    #if { $commLogState } {#line {log} {$commLogFile} }
};

#action {^%1 gang-say{s|}, '%3} {
  #var speaker %1;
  #if {"$speaker" == "You"} {#var speaker @prettyName{}};
  #if {&{$commsIgnore[%1]}} {#nop } {
    globalCommsListAdd {gang} {$speaker} {%3};
    #if { $commLogState } {#line {log} {$commLogFile} }
  }
}

#action {^%1 bonejack{s|}, '%3} {
  #var speaker %1;
  #if {"$speaker" == "You"} {#var speaker @prettyName{}};
  #if {&{$commsIgnore[%1]}} {#nop } {
    globalCommsListAdd {bj} {$speaker} {%3};
    #if { $commLogState } {#line {log} {$commLogFile} }
  }
}

#alias globalCommsListAdd {
  #var tmpLast $commsLine[-1];
  #var speaker {%2};
  #var tmpColor $commColors[%1];
  #var newComm {$tmpColor$speaker: %3};

  #replace {newComm} {'$} {};
  #if {"$newComm" == "$tmpLast" } { } {
    #all #list commsLine add {$newComm};
  }

  #list commsLine size tmp;
  #while  {$tmp > 4} {
    #all #list commsLine delete 1;
    #list commsLine size tmp;
  }
}

#alias commsUpdate {
  #loop {1} {4} {counter} {
    #echo {{<G05>%-${screenwidth}s<G00>} {-$counter}} {$commsLine[$counter]};
  };
}

#nop -- ---------------------------------------------------------------
#nop -- Tickers
#nop -- ---------------------------------------------------------------
#tick {commsUpdate} {#if { $commLogState } {#nop Log to file} {commsUpdate}} {1};

#alias globalListAdd {
  #all #list %1 add {%2};
}

#event {SCREEN RESIZE} { #var screenwidth %0; #var screenheight %1;}

#CLASs {comms} {close}
