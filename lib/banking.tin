#nop -- Banking tools
#nop -- vim: syntax=tt


#class {bankdb} {open}
#var remortCredits 0;
#read $db/$name-bank.db;
#class {bankdb} {close}

#class {bank} {kill}
#class {bank} {open}

#nop -- ---------------------------------------------------------------
#nop -- Actions and Triggers
#nop -- ---------------------------------------------------------------

#action {^Your current balance is %0 credits.} {#all #var bal[$name] %0 } {7}

#nop -- Fire Events
#action {^You withdraw %d credits.$} { event_raise {bankWithdraw} {%1} }
#action {^You deposit %d credits.$}  { event_raise {bankDeposit} {%1} }

#nop -- ---------------------------------------------------------------
#nop -- Events
#nop -- ---------------------------------------------------------------

event_register {remort} {resetCredits} { #var remortCredits 0; bankSave; }
event_register {bankWithdraw} {subCredits} { bankRCSub }
event_register {bankWithdraw} {updateCash} { bankAddCash }
event_register {bankDeposit} {addCredits} { bankRCAdd }
event_register {bankDeposit} {updateCash} { bankSubCash }
event_register {transport} {subTransportCredits} { subTransportCredits }
event_register {recall} {subRecallCredits} {subRecallCredits}

#nop -- ---------------------------------------------------------------
#nop -- Aliases
#nop -- ---------------------------------------------------------------

#alias bankSave { #class {bankdb} {write} {$db/$name-bank.db}; }
#alias bankRCAdd { #math {remortCredits} {$remortCredits + %2}; bankSave; }
#alias bankRCSub { #math {remortCredits} {$remortCredits - %2}; bankSave; }
#alias bankSubCash { #math {cash[$name]} {$cash[$name] - %2} }
#alias bankAddCash { #math {cash[$name]} {$cash[$name] + %2} }
#alias subRecallCredits { bankRCSub {recall} {450}  }

#alias subTransportCredits {
    #list {transportCommon} {find} {%3} {_result};
    #if {$_result > 0} {
        #if {"$curRoomTTH" == "Room of Summoning|aad3b693" } {
            bankRCSub {transportNBK} {500}
        };
        #elseif {"$curRoomTTH" == "Club Boom|0d0ccee6" } {
            bankRCSub {transportNBK} {500}
        };
        #else {
            bankRCSub {transportCommon} {1000}
        };

    } {
        #list {transportJacker} {find} {%3} {_result};
        #if {$_result == 0} {
            syslog {Unknown transport option, please add details to banking.tin}
        };
    }

}


#nop -- ---------------------------------------------------------------
#nop -- Variables
#nop -- ---------------------------------------------------------------
#list {transportCommon} {create} {Tek Angeles} {Casa Blanca} {Silicia} {Lost Wages} {Cyboria} {Club Boom} {Tek Angles};
#list {transportJacker} {create} {Tek Angeles HQ} {Combat Information Center} {Outpost Cyboria} {Base Station Sea Star}
    {Tactical Operations Center};


#class {bank} {close}

