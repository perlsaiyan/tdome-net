#nop -- Spell affects tracking
#nop -- vim:syntax=tt

#class {bufs} {kill}
#class {bufs} {open}


#list {_autogrow} {create} {shell} {body} {claws} {arms} {fingers};

#nop -- ---------------------------------------------------------------------
#nop -- Commands/Aliases intended for human use
#nop -- ---------------------------------------------------------------------

#nop -- show us what we're working with
#tab showBufs
#alias showBufs {

  #foreach {$registeredPlayers[%*]} {tmpChar} {
      #foreach {$bufs[$tmpChar][%*]} {tmpBuf} {
        #echo {<cfc>$tmpBuf is $bufs[$tmpChar][$tmpBuf]};
      };
  };

}

#alias ag {

    #list {_autogrow} {find} {%1} {result};
    #if { $result > 0 } {

        #if { "%2" == "on" } { #var _ag[%1] on; };
        #elseif { "%2" == "off" } { #var _ag[%1] off; };
        #else { #echo {<fca>Auto '%1' is '$_ag[%1]'}; };

    } {
      #if { "%1" == "" } {
        #foreach {$_autogrow[%*]} {_config} {#echo {<fca>Grow $_config is $_ag[$_config]}}
      } {
        #echo {<fca>Invalid loot option '%1'};
      }
    }
}

#nop -- ---------------------------------------------------------------------
#nop -- Control of buf status
#nop -- we call tickCalibrate on spell affects wearing off
#nop -- ---------------------------------------------------------------------
#alias {bufOn}  {
  global {bufs[$name][%1]} {1};
  #if { ! &bufStart[$name][%1] } { #var bufStart[$name][%1] {0} };
  #if {$bufStart[$name][%1] == 0} {
      global {bufStart[$name][%1]} {@getEpoch{}};
      global {bufSLevel[$name][%1]} {$curLevel[$name]};
  };
}
#alias {bufOff} {
  #echo {<cce>--==+ BUFEND: %1 (lev $bufSLevel[$name][%1]), lasted @delta{$bufStart[$name][%1]} +==--};
  global {bufs[$name][%1]} {0}; 
  global {bufStart[$name][%1]} {0};
  #if { @isAutoGrow{%1} } { grow %1 };
}


#nop -- ---------------------------------------------------------------------
#nop -- On/Off messages
#nop -- Use these to set/remove affect flags
#nop -- ---------------------------------------------------------------------
#nop -- Is on, set flags, don't set start time
#act {^You have some extra arms} {bufOn arms}
#act {^You feel blessed.} {bufOn bless}
#act {^You have long, sharp claws} {bufOn claws}
#act {^Your fist glows with a holy aura.} {bufOn fist}
#act {^You start glowing.} {bufOn sanct}

#nop -- Start buf

#nop -- Stop buf
#act {^You feel less righteous.} {bufOff bless}
#act {^Your ability to ward off good fades.} {bufOff protg}
#act {^The aura around your body fades.} {bufOff sanct}

#nop -- Converting to Events

#act {^A horde of eldritch spirits erupts from the {(ground|water)}, surging towards you to envelope you in an ebon miasma.} {event_raise buffOn eldritch}
#act {^Your limbs creak as they stretch out.$} {event_raise buffOn body}
#act {^You grow a big scaly shell!$} {event_raise buffOn shell}
#act {^You grow some sharp, nasty claws!} {event_raise buffOn claws}
#act {^Extra arms grow out from your body.} {event_raise buffOn arms}
#act {^Your fingers extend until they look like Wilt Chamberlain's.$} {event_raise buffOn fingers}

#act {^Your scaly shell slowly shrinks back into your skin.$} {event_raise buffEnd shell}
#act {^Your claws shrink back into your fingers.$} {event_raise buffEnd claws}
#act {^Your fingers droop back to their stubby selves.$} {event_raise buffEnd fingers}
#act {^Your extra arms suck back into your body.$} {event_raise buffEnd arms}
#act {^Your limbs are no longer quite so long.$} {event_raise buffEnd body}

#act {^It's hard to grow anything with this much stuff!} {event_raise buffOn fail}

#nop -- ---------------------------------------------------------------------
#nop -- Events
#nop -- ---------------------------------------------------------------------
event_register {buffEnd} {bufs} {bufOff %2}
event_register {buffOn} {bufs} {bufOn %2}


#nop -- ---------------------------------------------------------------------
#nop -- Functions
#nop -- ---------------------------------------------------------------------
#function isBuf { #format {result} {$bufs[$name][%1]} }
#function delta { #math {result} {(@getEpoch{} - %1) / $tickValue}}
#func isAutoGrow { #if {"$_ag[%1]" == "on"} { #var result 1 } { #var result 0 } }

#nop -- ---------------------------------------------------------------------
#nop -- Character Table
#nop -- ---------------------------------------------------------------------
#var bufTable {
  {shell} {⚇}
  {calm}  {☮}
}

#nop -- ---------------------------------------------------------------------
#nop -- These need to move to time
#nop -- ---------------------------------------------------------------------
#act {%1's scaly shell slowly shrinks back into his skin.} {bufOff shell}

#nop -- ---------------------------------------------------------------------
#nop -- TODO
#nop -- ---------------------------------------------------------------------
#nop The lightning stops.
#nop Kalik returns to her previous form.

#class {bufs} {close}
